buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}
plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "java" // java plugin is needed for the shadow plugin to work
    id "eclipse"
    id "maven-publish"
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

version = "1.16.x-2.0.0"
group = "dk.zlepper.itlt" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "itlt"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))
minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: "snapshot", version: "20200723-1.16.1"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            // The markers can be changed as needed.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            //property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property "forge.logging.console.level", "info"

            arg "-mixin.config=itlt.mixins.json"

            mods {
                itlt {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            // The markers can be changed as needed.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            //property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property "forge.logging.console.level", "info"

            arg "-mixin.config=itlt.mixins.json"

            mods {
                itlt {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            // The markers can be changed as needed.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            //property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property "forge.logging.console.level", "info"

            args "--mod", "itlt", "--all", "--output", file("src/generated/resources/")

            mods {
                itlt {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
}

repositories {
    maven {
        url = "https://cursemaven.com"
        content { includeGroup "curse.maven" }
    }
    mavenCentral()
    maven { url = "https://repository.mulesoft.org/nexus/content/repositories/public/" }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then "net.minecraft" it is assumed
    // that the dep is a ForgeGradle "patcher" dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:1.16.5-36.1.18"
    implementation "org.spongepowered:mixin:0.8.2"
    annotationProcessor "org.spongepowered:mixin:0.8.2:processor"

    // itlt uses the "image4j" library to read .ico files
    implementation "com.github.imcdonagh:image4j:0.7.2"
    shade "com.github.imcdonagh:image4j:0.7.2"

    // itlt uses the "Apache Commons Imaging" library to read .icns files
    /*implementation "org.apache.commons:commons-imaging:1.0-alpha2"
    shade "org.apache.commons:commons-imaging:1.0-alpha2"*/
    // due to it being such a big library and not supporting class removal, I've made a single method return null,
    // removed jpeg, tiff, examples and tests and then compiled it. This fork supports removing classes for formats we
    // don't use, therefore allowing us to drastically reduce the shrunk jar's filesize
    implementation files("thirdPartyLibs/commons-imaging-1.0-alpha2-custom.jar")
    shade files("thirdPartyLibs/commons-imaging-1.0-alpha2-custom.jar")
}

ext {
    MANIFEST = manifest {
        attributes([
                "Specification-Title": "itlt",
                "Specification-Vendor": "Paint_Ninja",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" : "Paint_Ninja",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "itlt.mixins.json",
                "Main-Class": "dk.zlepper.itlt.Main" // so that the pop-up window code works
        ])
    }
}

jar {
    manifest.from(MANIFEST)
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.shade]

    // Apache Commons Imaging needs to be put somewhere else to prevent a NoClassDefFoundError
    relocate "org.apache.commons.imaging", "${project.group}.shadow.org.apache.commons.imaging"

    manifest.from(MANIFEST)
}

reobf {
    shadowJar { }
}

task shrinkJar(type: Jar, dependsOn: reobfShadowJar) {
    ext {
        inputJar = file("${buildDir}/libs/${archivesBaseName}-${version}.jar")
        shadowGroup = project.group.replace('.','/') + "/shadow"
        apacheImagingFormats = "${shadowGroup}/org/apache/commons/imaging/formats"
        apacheImaging = "${shadowGroup}/org/apache/commons/imaging"
    }
    archiveClassifier.set("shrunk")
    from zipTree(inputJar)
    exclude "net/sf/image4j/example/**", "net/sf/image4j/test/**"

    exclude "${apacheImaging}/formats/gif/**", "${apacheImaging}/formats/psd/**", "${apacheImaging}/formats/rgbe/**"
    exclude "${apacheImaging}/formats/xbm/**", "${apacheImaging}/formats/xpm/**", "${apacheImaging}/formats/pnm/**"
    exclude "${apacheImaging}/formats/pcx/**", "${apacheImaging}/formats/dcx/**", "${apacheImaging}/formats/png/**"
    exclude "${apacheImaging}/formats/bmp/**", "${apacheImaging}/formats/wbmp/**", "${apacheImaging}/formats/ico/**"

    exclude "${apacheImaging}/common/itu_t4/**", "${apacheImaging}/common/mylzw/**", "${apacheImaging}/common/BasicCParser**"
    exclude "${apacheImaging}/common/RationalNumber**", "${apacheImaging}/common/GenericImageMetadata**"
    exclude "${apacheImaging}/common/ZlibDeflate**", "${apacheImaging}/common/PackBits**", "${apacheImaging}/icc/**"
    exclude "${apacheImaging}/palette/**", "${apacheImaging}/color/**", "${apacheImaging}/ImageDump**"
    exclude "${apacheImaging}/ColorTools**", "${apacheImaging}/internal/**"

    exclude "META-INF/LICENSE.txt", "META-INF/NOTICE.txt"

    manifest.from(MANIFEST)
}

//tasks.build.dependsOn reobfShadowJar
//jar.finalizedBy("reobfShadowJar")
tasks.build.dependsOn shrinkJar
jar.finalizedBy("shrinkJar")

mixin {
    add sourceSets.main, "itlt.refmap.json"
}